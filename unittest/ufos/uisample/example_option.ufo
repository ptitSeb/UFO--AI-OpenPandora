/**
 * @brief Unittest for event function from window node
 * Move it into the ui ufo script directory (/base/ufos/ui/), run the game, and run the command "ui_push unittest_option"
 */

window unittest_option
{
	{
		pos "100 100"
		size "700 340"
		dragbutton	true
		closebutton	true
		string		"_Unittest option"
		bgcolor		"0 0 0 1"
		timeout		1000
	}

	todo addSelectbox {
		pos "20 200"
		string "Add test with selectbox"
	}

	todo addSharedData {
		pos "20 220"
		string "Add test with shared data (should not be supported)"
	}

	tab tabReferenciel {
		{
			pos		"10 60"
			size	"680 40"
			cvar	*cvar:mn_unittest_option
			onChange {
				cmd "echo \"tabReferenciel <cvar:mn_unittest_option>\";"
			}
		}
		option normal {
			label	"Normal"
			value	"normal"
		}
		option disabled {
			label	"Disabled"
			value	"disabled"
			disabled	true
		}
		option hidden {
			label	"Hidden"
			value	"hidden"
			invis	true
		}
	}


	tab tabTest {
		{
			pos		"10 100"
			size	"680 40"
			cvar	*cvar:mn_unittest_option2
			onChange {
				cmd "echo \"tabReferenciel <cvar:mn_unittest_option2>\";"
			}
		}

		option normal {
			label	"Normal"
			value	"normal"
		}
		option disabled {
			label	"Disabled"
			value	"disabled"
		}
		option hidden {
			label	"Hidden"
			value	"hidden"
		}
	}
	button updateTab {
		pos		"20 150"
		size	"150 15"
		color	"0 0.5 0 1"
		selectcolor "1 1 1 1"
		string "Update tab"
		onClick {
			*node:parent.tabTest.disabled@disabled = true
			*node:parent.tabTest.hidden@invis = true
		}
	}

	button addTab {
		pos		"20 170"
		size	"150 15"
		color	"0 0.5 0 1"
		selectcolor "1 1 1 1"
		string "Add new option"
		onClick {
			call *node:parent.tabTest@createchild ( "option<cvar:nb>" , "option" )
			*node:parent.tabTest.option<cvar:nb>@value = ( *cvar:nb + 20 )
			*node:parent.tabTest.option<cvar:nb>@label = "Option <cvar:nb>"
			*cvar:nb = ( *cvar:nb + 1 )
		}
	}

	button resetTab {
		pos		"20 190"
		size	"150 15"
		color	"0 0.5 0 1"
		selectcolor "1 1 1 1"
		string "Reset tab"
		onClick {
			// reset static options
			*node:parent.tabTest.disabled@disabled = false
			*node:parent.tabTest.hidden@invis = false
			// it only remove dynamic child
			call *node:parent.tabTest@removeAllChild
			// reset the counter
			*cvar:nb = 0
		}
	}

	func onWindowOpened {
		*cvar:nb = 0
	}

	func onWindowClosed {
		delete *cvar:mn_unittest_option
		delete *cvar:mn_unittest_option2
		delete *cvar:nb
	}

}
